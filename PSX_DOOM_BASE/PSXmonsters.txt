//===========================================================================
//
// Nightmare Spectre
//
//===========================================================================
ACTOR PSXNightmareSpetre : Demon 3000
{
	Health 300
	PainChance 180
	Speed 10
	Radius 30
	Height 56
	Mass 400
	Monster
	+FLOORCLIP
	RenderStyle Subtract
	SeeSound "demon/sight"
	AttackSound "demon/melee"
	PainSound "demon/pain"
	DeathSound "demon/death"
	ActiveSound "demon/active"
	Obituary "$OB_DEMONHIT"
	States
	{
	Spawn:
		SARG AB 10 A_Look
		Loop
	See:
		SARG AABBCCDD 2 Fast A_Chase
		Loop
	Melee:
		SARG EF 8 Fast A_FaceTarget
		SARG G 8 Fast A_SargAttack
		Goto See
	Pain:
		SARG H 2 Fast
		SARG H 2 Fast A_Pain
		Goto See
	Death:
		SARG I 8
		SARG J 8 A_Scream
		SARG K 4
		SARG L 4 A_NoBlocking
		SARG M 4
		SARG N -1
		Stop
	Raise:
		SARG N 5
		SARG MLKJI 5
		Goto See
	}
}

//===========================================================================
//
// NightmareImp
//
//===========================================================================

ACTOR PSXNightmareImp : DoomImp 3007
{
	Health 60
	PainChance 100
	Speed 14
	RenderStyle Subtract
	Obituary "$OB_NMIMP"
	HitObituary "$OB_NMIMPHIT"
	States
	{
	Missile:
		TROO EF 9 A_FaceTarget
		TROO G 7 A_CustomComboAttack ("PSXNightmareImpBall", 32, random(1, 8)*3, "imp/melee", "", 1)
		Goto CheckTarget
	}
}

//===========================================================================
//
// NightmareImp fireball
//
//===========================================================================
ACTOR PSXNightmareImpBall : DoomImpBall
{
	Speed 16
	RenderStyle Translucent
	Alpha 0.6
	States
	{
	Spawn:
		NMBL AB 6 BRIGHT
		Loop
	Death:
		NMBL CDE 7 BRIGHT
		Stop
	}

}

// Dead imp ----------------------------------------------------------------

actor DeadPSXNightmareImp : PSXNightmareImp
{
	Skip_Super
	States
	{
	Spawn:
		Goto Super::Death+4
	}
}

// Dead demon --------------------------------------------------------------

actor DeadPSXNightmareSpetre : PSXNightmareSpetre
{
	Skip_Super
	States
	{
	Spawn:
		Goto Super::Death+5
	}
}
